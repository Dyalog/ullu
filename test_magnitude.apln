⍝ This Namespace includes tests for the function Magnitude which is represented by Monadic stile(|)
⍝
⍝ Magnitude:
⍝ Y may be any numeric array. R is numeric composed of the absolute (unsigned) values of Y.
⍝ Note that the magnitude of a complex number a+⍳b is defined to be √(a^2+b^2).
:Namespace test_magnitude
    Assert←#.unittest.Assert
    
    ⍝ evaluate each test with normal, empty and multiple shaped data
    ∇ tRes←tData RunVariations exp ;actualR;actualRE;expectedR;left;right;res;tID;tCmt;p;shape;shapeW0;actualRS
        (expectedR p)←exp
        (tID tCmt)←tData
        tRes←⍬

        ⍝ normal
        actualR←|p
        res←expectedR≡actualR
        tRes,←tData Assert res

        ⍝ empty
        actualRE←(0⍴⊂p) ⍝ generating empty array
        res←(|(0⍴⊂p))≡actualRE
        tRes,←('Empty',tID) tCmt Assert res

        ⍝ different shapes
        shape←?4/4
        actualRS←|shape⍴actualR
        res←(shape⍴expectedR)≡actualRS
        tRes,←('Multiple',tID) tCmt Assert res

        ⍝ different shapes with 0 in shape
        shapeW0←(0@(?4))shape
        actualRS←|shapeW0⍴p
        res←(shapeW0⍴0) ≡ actualRS
        tRes,←('ShapeW0',tID) tCmt Assert res
    ∇

    ∇ r←Test_Magnitude_General ;bool;i1;i2;i3;dbl;fl;Hdbl;Hfl;fr_dbl;fr_decf;getMagGen;case;data;d;testDesc;desc;fr;⎕FR
        fr_dbl←645
        fr_decf←1287

        bool←0 1                                      ⍝ 11: 1 bit Boolean type arrays
        i1←{⍵,-⍵}⍳120                                 ⍝ 83: 8 bits signed integer
        i2←{⍵,-⍵}10000+⍳1000                          ⍝ 163: 16 bits signed integer
        i3←{⍵,-⍵}100000+⍳100                          ⍝ 323: 32 bits signed  integer
        dbl←{⍵,-⍵}1000.5+⍳100                         ⍝ 645: 64 bits Floating
        ⍝ Hdbl and Sdbl is 645 but higher order and lower order
        Hdbl←{⍵,-⍵}1E14+(2×⍳50)
        Sdbl←{⍵,-⍵}(⍳10)×1E¯40
        ⍝ Hfl and Sfl is 1287 but higher order and lower order
        ⎕FR←fr_decf                                   ⍝ use ⎕FR=1287
        fl←{⍵,-⍵}1000.5+⍳100                          ⍝ 1287: 128 bits Decimal
        Hfl←{⍵,-⍵}2E29+(1E16×⍳10)
        Sfl←{⍵,-⍵}(⍳10)×1E¯40
        ⎕FR←fr_dbl                                    ⍝ revert ⎕FR=645

        r←⍬
        getMagGen←{⍵>0:⍵⋄¯1×⍵}
        testDesc←{'for ',case, '& ⎕FR:', ⎕FR}

        :For fr :In 1 2
            ⎕FR←fr⊃fr_dbl fr_decf
            :For case :In 'bool' 'i1' 'i2' 'i3' 'dbl' 'Hdbl' 'Sdbl' 'fl' 'Hfl' 'Sfl'
                data←⍎case
                desc←testDesc⍬
                :If case≢'bool'
                    r,←'T1' desc RunVariations ({⍵,⍵}{((≢⍵)÷2)↑⍵}data) data ⍝ all positive values are chosen
                    d←data[?≢data]
                    r,←'T2' desc RunVariations (getMagGen d) d ⍝ custom function does the same operation
                :Else
                    r,←'Tb1' desc RunVariations (0 1) bool
                    r,← 'Tb2' desc RunVariations 0 (⊃bool)
                :EndIf
            :EndFor
        :EndFor
    ∇

    ∇ r←Test_Magnitude_Cmplx ;cmplx;Hcmplx;getMagCmplx;d;case;data;testDesc;desc
        cmplx←{⍵,-⍵}(0J1×⍳10)+⌽⍳10            ⍝ 1289: 128 bits Complex
        Hcmplx←{⍵,-⍵}(1E13J1E13×⍳20)          ⍝ 1289 but higher order to test for CT value
        ⍝ not testing for decimal values because it is not recommended in documentation
        
        getMagCmplx←{0.5*⍨+.×⍨9 11○⍵}
        r←⍬
        testDesc←{'for ',case}

        ⍝ not testing for fr values because using fr=1287 is not recommended in docs
        :For case :In 'cmplx' 'Hcmplx'
            data←⍎case
            desc←testDesc⍬
            r,←'Tc1' desc RunVariations (getMagCmplx¨data) data

            :For d :In data
                case←d
                desc←testDesc⍬
                r,←'Tc2' desc RunVariations (getMagCmplx d) d
            :EndFor
        :EndFor
    ∇
:EndNamespace