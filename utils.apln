:Namespace utils
    ⍝ constants
    ct_default←1E¯14    ⍝ ⎕CT values
    dct_default←1E¯28

    fr_dbl←645          ⍝ ⎕FR values
    fr_decf←1287

    io_default←1        ⍝ ⎕IO values
    io_0←0

    div_0←0             ⍝ ⎕DIV values
    div_1←1

    ⍝ utility functions
    shuffle←{⊂⍤?⍨∘≢⌷⍵}              ⍝ completely Random shuffle
    intertwine←{(⊂⍋∊⍳∘≢¨⍺ ⍵)⌷⍺⍪⍵}   ⍝ perfectly intertwine two arrays
    hashArray←1500⌶                 ⍝ pre-hash array
    stripToSameLen←{(⍺(⌊⍥≢)⍵)↑¨⍺ ⍵} ⍝ get two arrays to the length of the shorter array
    numOrChar←{326(=-⍨2|⊢)⎕DR ⍵}    ⍝ Type of ⍵ (¯1:mixed, 0:character, 1:numeric)
    change0sto1←{1@(0∘=)⍵}          ⍝ Replacing zeroes in ⍵ with 1
    ints←{ ⍝ ⍺ integers of size ⍵ (bits), excluding the booleans
          ⎕IO←0
          ⍺⍴0 1~⍨(2*⍵-1)-⍨?⍺⍴2*⍵
    }
    subsets←{⌿∘⍵¨↓⌽⍉2⊥⍣¯1⊢¯1+⍳2*≢⍵} ⍝ (From APLCart) Powerset: All subsets of ⍵, including the empty set (0⌿⍵) and ⍵ itself

    ⍝ Interpreter version information
    isClassic←82=⎕dr''
    width←{2×⎕SIZE '⍵'}⍬
    isBigEndian←0=⊃11 ⎕DR 128
    version←{⍎(2>+\⍵='.')/⍵}⊃1↓'.'⎕wg'APLVersion'
    verifyDotNetInterface←(2250⌶)0
    doSlowTests←((,'1')≡ 2 ⎕NQ #'GetEnvironment' 'DYALOG_QA_SLOW_TESTS')

    ∇ (min max)←NumericMinMax type;RealMinMax;bits
    ⍝ Return the min and max value of a number with element
    ⍝ type 'type'
      RealMinMax←{
          ⎕FR←⍵
          (⌈⌿,⌊⌿)⍬
      }
      :Select type
      :Case 11
          (min max)←0 1
      :CaseList 83 163 323
          bits←⌊type÷10
          min←-2*bits-1
          max←¯1+2*bits-1
      :CaseList 645 1287
          (min max)←RealMinMax type
      :Case 1289
          (min max)←{⍵+¯11○⍵}RealMinMax 645
      :Else
          'Missing case'⎕SIGNAL 11
      :EndSelect
     
    ∇

    :Namespace Simd
        ⎕IO←1
        ∇ r←Category c;m
          m←(⊂c)≡¨⊃21165⌶0
          :If 1≠+⌿m
              ('No such SIMD category: ',c)⎕SIGNAL 11
          :EndIf
          r←⍸m
        ∇

        ∇ which Enable id;m
          m←6⊃21165⌶0
          m[id;]←0 ⍝ Disable all in the category
          m[id;which]←1 ⍝ Enable those we want
          m(21165⌶)1
        ∇

        ∇ Reset;m
          m←5⊃21165⌶0 ⍝ Reset the enabled optimisations to the defaults
          m(21165⌶)1
        ∇

        ∇ r←AllCombinations id;all
          r←##.subsets⍸,(5⊃21165⌶0)[id;]
        ∇
    :EndNamespace
:EndNamespace
