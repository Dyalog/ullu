:Namespace test_membership
    assert←#.unittest.assert
    ⎕RL←#.unittest.randLink

    ⍝ Read more about DR: https://help.dyalog.com/latest/#Language/System%20Functions/Data%20Representation%20Monadic.htm
    ⍝ Read more about CT and DCT: https://help.dyalog.com/latest/#Language/System%20Functions/ct.htm
    ∇ r←Test_Membership ;w;n;r;data;case;data2;case2;type;bool;i1;char1;char2;i2;char3;i3;ptr;dbl;fl;cmplx;Hdbl;Hfl;Hcmplx;d;ct;⎕CT;⎕DCT;⎕FR;ct_default;dct_default;testDesc
        bool←0 1                                      ⍝ 11: 1 bit Boolean type arrays
        i1←¯60+⍳120                                   ⍝ 83: 8 bits signed integer
        char1←⎕UCS ⍳255                               ⍝ 80: 8 bits character
        char2←⎕UCS (1000+⍳100)                        ⍝ 160: 16 bits character
        i2←{⍵,-⍵}10000+⍳1000                          ⍝ 163: 16 bits signed integer
        char3←⎕UCS (100000+⍳100)                      ⍝ 320: 32 bits character
        i3←{⍵,-⍵}100000+⍳100                          ⍝ 323: 32 bits signed  integer
        ptr←2,/⎕A                                     ⍝ 326: Pointer (32-bit or 64-bit as appropriate)
        dbl←{⍵,-⍵}i3+0.1                              ⍝ 645: 64 bits Floating
        ⎕FR←1287 ⋄ fl←{⍵,-⍵}i3+0.01 ⋄ ⎕FR←645         ⍝ 1287: 128 bits Decimal
        cmplx←{⍵,-⍵}(0J1×⍳100)+⌽⍳100                  ⍝ 1289: 128 bits Complex
        ⍝ Hdbl is 645 but higher order to test for CT value
        ⍝ intervals of 2 are chosen because CT for these numbers +1 and -1
        ⍝ come under the region of tolerant equality
        Hdbl←{⍵,-⍵}1E14+(2×⍳50)
        ⍝ Hfl is 1287 but higher order to test for CT value
        ⍝ far intervals are chosen for non overlap 
        ⍝ with region of tolerant equality
        ⎕FR←1287 ⋄ Hfl←{⍵,-⍵}2E15+(500×⍳10) ⋄ ⎕FR←645 
        Hcmplx←{⍵,-⍵}(1E14J1E14×⍳20)                  ⍝ 1289 but higher order to test for CT value
        
        ct_default←1E¯14
        dct_default←1E¯28

        r←⍬
        testDesc←{'for ',case,{0∊⍴case2:'',⍵⋄' , ', case2,⍵},' & ⎕CT ⎕DCT:',⎕CT,⎕DCT}

        :For ct :In 1 0 
            (⎕CT ⎕DCT)←ct × ct_default dct_default ⍝ set comparision tolerance

            :For case :In 'i1' 'i2' 'i3' 'char1' 'char2' 'char3' 'ptr' 'dbl' 'fl' 'cmplx' 'Hdbl' 'Hfl' 'Hcmplx'
                data←⍎case
                ⍝ Cross type tests
                :For case2 :In 'i1' 'i2' 'i3' 'char1' 'char2' 'char3' 'ptr' 'dbl' 'fl' 'cmplx' 'Hdbl' 'Hfl' 'Hcmplx'
                    data2←⍎case2
                    desc←testDesc⍬
                    :If (data≡data2)
                        r,←assert (((≢data)⍴1) ≡ (data∊data2)) 'tcross0' desc
                        :Continue
                    :EndIf
                    r,←assert (((≢data)⍴0) ≡ (data∊data2)) 'tcross1' desc ⍝ no match, all return 0
                    r,←assert (((≢data2)⍴0) ≡ (data2∊data)) 'tcross2' desc ⍝ no match, all return 0
                    r,←assert (((≢data)⍴1) ≡ (data∊data,data2)) 'tcross3' desc ⍝ first data match, returns 1
                    r,←assert ((((≢data)⍴1),((≢data2)⍴0))≡((data,data2)∊data)) 'tcross4' desc ⍝ data match, data2 no match
                :EndFor
                case2←⍬ ⍝ disposing case2 for testDesc
                desc←testDesc⍬

                ⍝ same type tests
                r,←assert ((data[?≢data])∊data) 't1' desc                  ⍝ random element is found
                r,←assert (((((≢data)-1)⍴0), 1)≡data∊(¯1↑data)) 't2' desc  ⍝ All elements return 0 except last
                r,←assert (((≢data)⍴1)≡data∊data) 't3' desc                ⍝ all elements return 1
                r,←assert (0≡5E50∊data) 't4' desc                          ⍝ huge element outside set is not found

                ⍝ tests with comparision tolerance
                d←data[?≢data]
                :If ct
                    ⍝ case for CT default
                    :If (data≡Hdbl) ∨ (data≡Hfl) ∨ (data≡Hcmplx)
                        ⍝ next number is equal for Hdbl as it comes under the region of tolerant equality
                        r,←assert (1≡(d∊d+1)) 'CTDefault' desc
                    :Else
                        :Continue ⍝ not relevant for other datatypes
                    :EndIf
                :Else
                    :If ((⎕DR data) ∊ 80 160 320 326)
                        :continue
                    :EndIf
                    r,←assert (0≡(d∊d+1)) 'CTZero' desc
                :EndIf
            :EndFor

            ⍝ Booleans need special tests
            case←'bool'
            desc←testDesc⍬

            r,←assert (0∊0) 'tb1' desc
            r,←assert (~0∊1) 'tb2' desc
            r,←assert (1 1≡bool∊bool) 'tb3' desc
            
            ⍝ Boolean cross type tests
            :For case2 :In 'i1' 'i2' 'i3' 'char1' 'char2' 'char3' 'ptr' 'dbl' 'fl' 'cmplx'  'Hdbl' 'Hfl' 'Hcmplx' ⍝ bool requires separate cross tests because of overlap with i1
                data2←⍎case2
                desc←testDesc⍬
                :If (83≠type←⎕DR data2) ⍝ special case because of overlap
                        r,←assert (((≢bool)⍴0) ≡ (bool∊data2)) 'tcrossBool1' desc ⍝ All elements return 0
                        r,←assert (((≢data2)⍴0) ≡ (data2∊bool)) 'tcrossBool2' desc ⍝ All elements return 0
                :Else
                    r,←assert (((≢bool)⍴1) ≡ (bool∊data2)) 'tcrossBooli1' desc ⍝ All elements return 1
                    r,←assert (((⊣,(⌽⊣))((1↓((≢i1)÷2)⍴0),1)) ≡ (data2∊bool)) 'tcrossBooli2' desc ⍝ overlaping 0 1 returns 1
                :EndIf
            :EndFor
            ⍝ disposing case for testDesc
            case←⍬
            case2←⍬
        :EndFor
    ∇
:EndNamespace
